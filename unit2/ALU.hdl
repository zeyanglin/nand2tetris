// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

// Multiplexor:
// if (sel = 0) out = a, else out = b

// True: 11111111
// False: 00000000

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    // zx(if zx==1, all x bits to zero)
    Mux16(a=x, b=false, sel=zx, out=xz);

    // nx(if nx==1, output notx)
    Not16(in=xz , out=notxz );
    Mux16(a=xz , b=notxz , sel=nx , out=xn );

    // zy(if zy==1, all y bits to zero)
    Mux16(a=y, b=false, sel=zy, out=yz);

    // ny(if ny==1, output noty)
    Not16(in=yz , out=notyz );
    Mux16(a=yz , b=notyz , sel=ny , out=yn );

    // f(if f==0, output x&y; if f==1, output x+y)
    And16(a=xn , b=yn , out=xnandyn );
    Add16(a =xn , b =yn , out =sumxnyn );
    Mux16(a=xnandyn , b=sumxnyn , sel=f , out=fout );

    // no(if no==1, out = !out)
    Not16(in=fout , out=nfout );
    Mux16(a=fout , b=nfout , sel=no , out=out, out[15]= out15, out[0..7]= out0to7, out[8..15]= out8to15);

    // zr
    // 如果or16结果是[00000..00000],那么tempout就都是0，zr输出1
    Or8Way(in=out0to7, out=lowOr);
    Or8Way(in=out8to15, out=highOr);
    Or(a=lowOr, b=highOr, out=anyOne); // anyOne = 1 if any bit is 1
    Not(in=anyOne, out=zr);            // zr = 1 if all 0

    // ng 
    // 补码中，最高位(out[15]如果是1，代表负数，负数则输出ng==1)
    And(a=out15 , b=true , out=ng );
}